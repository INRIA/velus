(* The stopwatch example from
   Colaco, Pagano, Pouzet,
   "Scade 6: A Formal Language for Embedded Critical Software Development" *)

node stopwatch(stst, rst, set, md : bool)
returns (hh, mm, ss : int; isLap : bool)
var
    isStart : bool; -- is chrono started ?
    isWatch : bool; -- is in clock mode ?
    isSet, isSetEnd : bool;
    m, s, d : int; -- chrono timers
    wh, wm, w, ws : int; -- clock timers
let
    last m = 0;
    last s = 0;
    last d = 0;
    last wh = 0;
    last wm = 0;
    last w = 0;
    last ws = 0;
    w = 0 fby (w + 1) mod 100;
    ws = 0 -> (if (w < (0 fby w))
               then (0 fby w) + 1 else (0 fby w)) mod 60;

    automaton initially Stop
    state Stop do
      isStart = false;
    unless stst and not isWatch continue Start
         | rst and not isLap and not isWatch then Stop

    state Start do
      isStart = true;
      d = (last d + 1) mod 100;
      s = (if d < last d then last s + 1 else last s) mod 60;
      m = if s < last s then last m + 1 else last m;
    unless stst and not isWatch continue Stop
    end;

    automaton initially Count
    state Count do
      isSet = false;
      isSetEnd = false;
      wm = 0 -> (if ws < last ws then last wm + 1 else last wm) mod 60;
      wh = 0 -> (if wm < last wm then last wh + 1 else last wh) mod 24;
    until set and isWatch then Set

    state Set do
      isSet = true;
      automaton initially SetHours
      state SetHours do
        isSetEnd = false;
        wh = (if stst then last wh + 1 else if rst then last wh + 23 else last wh) mod 24
      until set and isWatch then SetMinutes
      state SetMinutes do
        isSetEnd = false;
        wm = (if stst then last wm + 1 else if rst then last wm + 23 else last wm) mod 24
      until set and isWatch then SetEnd
      state SetEnd do isSetEnd = true
      end
    until isSetEnd continue Count
    end;

    automaton initially DisplayWatch
    state DisplayWatch do
      isWatch = true;
      isLap = false;
      hh, mm, ss = (wh, wm, ws)
    unless md and not isSet continue DisplayStopWatch
    state DisplayStopWatch
    var lm, ls, ld : int; do
      last lm = 0;
      last ls = 0;
      last ld = 0;
      isWatch = false;
      hh, mm, ss = (lm, ls, ld);

      automaton initially StopWatch
      state StopWatch do
        isLap = false;
        lm, ls, ld = (m, s, d)
      until rst and isStart then Lap
      state Lap do
        isLap = true;
      until rst then StopWatch
      end

    unless md and not isSet continue DisplayWatch
    end
tel
