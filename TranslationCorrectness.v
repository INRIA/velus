Require Import List.
Import List.ListNotations.
Open Scope list_scope.

Require Import Rustre.Common.
Open Scope positive.

Require Import Rustre.Heap.
Require Import Rustre.Dataflow.
(* TODO: these should go *)
Require Import Rustre.Dataflow.IsVariable.Decide.
Require Import Rustre.Dataflow.IsDefined.Decide.

(* ** Definitions on imperative statements *)

Require Import Rustre.Minimp.
Require Import Rustre.Translation.
Require Import Rustre.Translation.Proper.
Require Import Rustre.Translation.IsPresent.
Require Import Rustre.Translation.MemoryCorres.


(* TODO:
   - Assume we are given a list of dataflow equations eqs satisfying
        sch_eqs (memories eqs) PS.empty (defined eqs) eqs
   - Show that the step function generated by translate_eqs is correct,
     i.e., that its semantics correspond to the 'held semantics' of the
     original dataflow equations.
   - The 'held semantics' use holdR rather than fbyR for defining 'fby'.
   - Basic invariant for a single pass (to show by induction along the list
     of dataflow equations/sequence of conditional assignments):
        forall x,
             (PS.In x (PS.inter memories written) -> M x = H x (S n))
          /\ (PS.In x (PS.diff memories written) -> M x = H x n)
          /\ (PS.In x (PS.diff written memories) ->
              H x n = present c -> p x = c)

   - Afterward, this must be extended to run (i.e., forall n from 0).
   - and we must switch back to the standard fby semantics for instants
     where a clock is active.
 *)


Lemma exp_eval_tovar:
  forall x v menv env memories,
    exp_eval menv env (tovar memories x) v
    <-> (exp_eval menv env (State x) v /\ PS.In x memories)
        \/ (exp_eval menv env (Var x) v /\ ~PS.In x memories).
Proof.
  split; intro Heval;
  destruct In_dec with x memories as [Hxm|Hxm];
    pose proof Hxm as Hxmt;
    apply PS.mem_spec in Hxmt || apply mem_spec_false in Hxmt;
    unfold tovar in *;
    rewrite Hxmt in *;
    intuition.
Qed.

Lemma stmt_eval_translate_eqns_cons:
  forall prog mems menv env menv' env' eq eqs,
    stmt_eval prog menv env (translate_eqns mems (eq :: eqs)) (menv', env')
    <->
    (exists menv'' env'',
        stmt_eval prog menv env (translate_eqns mems eqs) (menv'', env'')
        /\ stmt_eval prog menv'' env'' (translate_eqn mems eq) (menv', env')).
Proof. (* TODO: redo proof *)
  split.
  - intro H.
    unfold translate_eqns in H.
    simpl in H.
    apply stmt_eval_fold_left_shift in H.
    destruct H as [menv'' [env'' [H1 H2]]].
    exists menv'', env''.
    split; [now apply H1|].
    inversion_clear H2.
    inversion H0.
    subst.
    exact H.
  - intro H.
    destruct H as [menv'' [env'' [H1 H2]]].
    unfold translate_eqns.
    simpl.
    apply stmt_eval_fold_left_shift.
    exists menv'', env''.
    split; [now apply H1|].
    eapply Icomp. apply H2.
    apply Iskip.
Qed.

Lemma stmt_eval_Control_fwd:
  forall prog menv env mems c s menv' env',
    stmt_eval prog menv env (Control mems c s) (menv', env')
    -> (Is_present_in mems menv env c
        /\ stmt_eval prog menv env s (menv', env'))
       \/ (Is_absent_in mems menv env c
           /\ menv' = menv /\ env' = env).
Proof.
  Hint Constructors Is_present_in Is_absent_in.
  intros prog menv env mems c s menv' env' Hs.
  revert s Hs.
  induction c; [now intuition|].
  intros s Hs.
  simpl in Hs.
  destruct b;
    specialize (IHc _ Hs); clear Hs;
    destruct IHc as [[Hp Hs]|[Hp [Hmenv Henv]]];
    try inversion_clear Hs;
  (left; now intuition)
   || (right;
        repeat progress
               match goal with
               | H: stmt_eval _ _ _ Skip _ |- _ => inversion H; subst; clear H
               | Hp: Is_present_in _ _ _ _,
                     He: exp_eval _ _ _ _ |- Is_absent_in _ _ _ _
                 => apply IsAbs2 with (1:=Hp) (2:=He)
               | _ => intuition
               end).
Qed.

Lemma stmt_eval_Control:
  forall prog mems menv env ck stmt,
    (Is_absent_in mems menv env ck
     -> stmt_eval prog menv env (Control mems ck stmt) (menv, env))
    /\
    (forall menv' env',
       Is_present_in mems menv env ck
       -> stmt_eval prog menv env stmt (menv', env')
       -> stmt_eval prog menv env (Control mems ck stmt) (menv', env')).
Proof.
  intros prog mems menv env ck.
  induction ck; intro s; split.
  - now inversion 1.
  - intros menv' env' Hp Hs; exact Hs.
  - inversion_clear 1 as [? ? ? Hp|? ? ? ? Hp Hexp Hneq];
    destruct b;
    try (now apply IHck with (1:=Hp));
    apply not_eq_sym in Hneq;
      (apply Bool.not_true_is_false in Hneq
       || apply Bool.not_false_is_true in Hneq);
      subst;
      apply IHck with (1:=Hp);
      (apply Iifte_false with (1:=Hexp)
       || apply Iifte_true with (1:=Hexp));
      constructor.
  - inversion_clear 1 as [|? ? ? Hp Hexp];
    intro Hs;
    destruct b;
    apply IHck with (1:=Hp);
    [ now apply Iifte_true with (1:=Hexp) (2:=Hs)
    | now apply Iifte_false with (1:=Hexp) (2:=Hs) ].
Qed.

Lemma stmt_eval_Control_absent:
  forall prog mems menv env ck stmt,
    Is_absent_in mems menv env ck
    -> stmt_eval prog menv env (Control mems ck stmt) (menv, env).
Proof.
  apply stmt_eval_Control.
Qed.

Lemma stmt_eval_Control_present:
  forall prog mems menv env ck stmt menv' env',
    Is_present_in mems menv env ck
    -> stmt_eval prog menv env stmt (menv', env')
    -> stmt_eval prog menv env (Control mems ck stmt) (menv', env').
Proof.
  apply stmt_eval_Control.
Qed.

Lemma stmt_eval_translate_cexp_menv_inv:
  forall prog menv env mems x menv' env' ce,
    stmt_eval prog menv env (translate_cexp mems x ce) (menv', env')
    -> menv' = menv.
Proof.
  intros prog menv env mems x menv' env'.
  induction ce;
  (apply IHce || inversion_clear 1); auto.
Qed.

Lemma stmt_eval_translate_cexp_env_add:
  forall prog menv env mems x menv' env' ce,
    stmt_eval prog menv env (translate_cexp mems x ce) (menv', env')
    -> exists c, env' = PM.add x c env.
Proof.
  intros prog menv env mems x menv' env'.
  induction ce;
    (apply IHce || inversion_clear 1); auto.
  exists v; rewrite <- H1; intuition.
Qed.

Lemma not_Is_defined_in_eq_stmt_eval_menv_inv:
  forall prog x eq menv env mems menv' env',
    ~Is_defined_in_eq x eq
    -> stmt_eval prog menv env (translate_eqn mems eq) (menv', env')
    -> mfind_mem x menv' = mfind_mem x menv.
Proof. (* TODO: Tidy proof *)
  intros prog x eq menv env mems menv' env' Hneq Heval.
  destruct eq as [y cae|y f lae|y v0 lae].
  - destruct cae.
    simpl in Heval.
    apply stmt_eval_Control_fwd in Heval; destruct Heval as [Heval|Heval];
    destruct Heval as [Heval1 Heval2].
    apply stmt_eval_translate_cexp_menv_inv in Heval2.
    rewrite Heval2. intuition.
    destruct Heval2 as [Hmenv]; rewrite Hmenv; intuition.
  - destruct lae.
    simpl in Heval.
    apply stmt_eval_Control_fwd in Heval; destruct Heval as [Heval|Heval];
    destruct Heval as [Hipi Heval].
    inversion_clear Heval.
    rewrite <- H2.
    reflexivity.
    destruct Heval as [Hmenv Henv]; rewrite Hmenv; intuition.
  - apply not_Is_defined_in_eq_EqFby in Hneq.
    unfold translate_eqn in Heval.
    destruct lae.
    apply stmt_eval_Control_fwd in Heval; destruct Heval as [Heval|Heval];
    destruct Heval as [Hipi Heval].
    inversion_clear Heval.
    rewrite <- H0.
    unfold mfind_mem, madd_mem.
    simpl; rewrite PM.gso; [intuition | apply Hneq].
    destruct Heval as [Hmenv Henv]; rewrite Hmenv; intuition.
Qed.

Lemma not_Is_defined_in_eq_stmt_eval_mobj_inv:
  forall prog x eq menv env mems menv' env',
    ~Is_defined_in_eq x eq
    -> stmt_eval prog menv env (translate_eqn mems eq) (menv', env')
    -> mfind_inst x menv' = mfind_inst x menv.
Proof. (* TODO: Tidy proof *)
  intros prog x eq menv env mems menv' env' Hneq Heval.
  destruct eq as [y cae|y f lae|y v0 lae].
  - destruct cae.
    simpl in Heval.
    apply stmt_eval_Control_fwd in Heval; destruct Heval as [Heval|Heval];
    destruct Heval as [Heval1 Heval2].
    apply stmt_eval_translate_cexp_menv_inv in Heval2.
    rewrite Heval2. intuition.
    destruct Heval2 as [Hmenv]; rewrite Hmenv; intuition.
  - destruct lae.
    simpl in Heval.
    apply stmt_eval_Control_fwd in Heval; destruct Heval as [Heval|Heval];
    destruct Heval as [Hipi Heval].
    + inversion_clear Heval.
      rewrite <- H2.
      destruct (ident_eq_dec x y) as [Hxy|Hxny].
      * rewrite Hxy in Hneq; exfalso; apply Hneq; constructor.
      * rewrite mfind_inst_gso; [reflexivity|assumption].
    + destruct Heval as [HR1 HR2]; rewrite HR1; reflexivity.
  - unfold translate_eqn in Heval.
    destruct lae.
    apply stmt_eval_Control_fwd in Heval; destruct Heval as [Heval|Heval];
    destruct Heval as [Hipi Heval].
    inversion_clear Heval.
    rewrite <- H0.
    unfold mfind_inst, madd_mem.
    reflexivity.
    destruct Heval as [Hmenv Henv]; rewrite Hmenv; intuition.
Qed.

Lemma not_Is_variable_in_eq_stmt_eval_env_inv:
  forall prog x eq menv env mems menv' env',
    ~Is_variable_in_eq x eq
    -> stmt_eval prog menv env (translate_eqn mems eq) (menv', env')
    -> PM.find x env' = PM.find x env.
Proof.
  intros prog x eq menv env mems menv' env' Hnd Heval.
  destruct eq as [y e|y f e|y v0 e].
  - unfold translate_eqn in Heval.
    destruct e.
    apply stmt_eval_Control_fwd in Heval;
      destruct Heval as [[Hipi Heval]|[Habs [Hmenv Henv]]];
      [|rewrite Henv; reflexivity].
    apply stmt_eval_translate_cexp_env_add in Heval.
    destruct Heval; rewrite H; rewrite PM.gso;
    [reflexivity|intro HR; rewrite HR in *; apply Hnd; constructor].
  - simpl in Heval; destruct e.
    apply stmt_eval_Control_fwd in Heval; destruct Heval as [Heval|Heval];
    destruct Heval as [Heval1 Heval2].
    inversion_clear Heval2.
    rewrite <- H3.
    rewrite PM.gso; [reflexivity|].
    intro Hxy; apply Hnd; rewrite Hxy; constructor.
    destruct Heval2 as [Hmenv Henv]; rewrite Henv; intuition.
  - simpl in Heval; destruct e.
    apply stmt_eval_Control_fwd in Heval; destruct Heval as [Heval|Heval];
    destruct Heval as [Heval1 Heval2].
    inversion Heval2; intuition.
    destruct Heval2 as [Hmenv Henv]; rewrite Henv; intuition.
Qed.

Lemma not_Is_defined_in_eq_stmt_eval_env_inv:
  forall prog x eq menv env mems menv' env',
    ~Is_defined_in_eq x eq
    -> stmt_eval prog menv env (translate_eqn mems eq) (menv', env')
    -> PM.find x env' = PM.find x env.
Proof.
  intros prog x eq menv env mems menv' env' Hidi Hstmt.
  apply not_Is_defined_in_eq_not_Is_variable_in_eq in Hidi.
  now apply not_Is_variable_in_eq_stmt_eval_env_inv with (1:=Hidi) (2:=Hstmt).
Qed.

Lemma stmt_eval_translate_eqns_menv_inv:
  forall prog menv env mems eqs menv' env',
    stmt_eval prog menv env (translate_eqns mems eqs) (menv', env')
    -> (forall x, ~Is_defined_in x eqs ->
                  mfind_mem x menv' = mfind_mem x menv).
Proof.
  induction eqs as [ |eq].
  + inversion_clear 1; reflexivity.
  + intros menv' env' Heval x Hnmem.
    apply not_Is_defined_in_cons in Hnmem.
    destruct Hnmem as [H0 H1].
    apply stmt_eval_translate_eqns_cons in Heval.
    destruct Heval as [menv'' [env'' [Heval0 Heval1]]].
    apply IHeqs with (x:=x) (2:=H1) in Heval0.
    apply not_Is_defined_in_eq_stmt_eval_menv_inv with (1:=H0) in Heval1.
    rewrite Heval1, Heval0.
    reflexivity.
Qed.

Lemma stmt_eval_translate_eqns_minst_inv:
  forall prog menv env mems eqs menv' env',
    stmt_eval prog menv env (translate_eqns mems eqs) (menv', env')
    -> (forall x, ~Is_defined_in x eqs ->
                  mfind_inst x menv' = mfind_inst x menv).
Proof.
  induction eqs as [ |eq].
  + inversion_clear 1; reflexivity.
  + intros menv' env' Heval x Hnmem.
    apply not_Is_defined_in_cons in Hnmem.
    destruct Hnmem as [H0 H1].
    apply stmt_eval_translate_eqns_cons in Heval.
    destruct Heval as [menv'' [env'' [Heval0 Heval1]]].
    apply IHeqs with (x:=x) (2:=H1) in Heval0.
    apply not_Is_defined_in_eq_stmt_eval_mobj_inv with (1:=H0) in Heval1.
    rewrite Heval1, Heval0.
    reflexivity.
Qed.

Lemma stmt_eval_translate_eqns_env_inv:
  forall prog menv env mems eqs menv' env',
    stmt_eval prog menv env (translate_eqns mems eqs) (menv', env')
    -> (forall x, ~Is_variable_in x eqs ->
                  PM.find x env' = PM.find x env).
Proof.
  induction eqs as [ |eq].
  + inversion_clear 1; reflexivity.
  + intros menv' env' Heval x Hndef.
    apply not_Is_variable_in_cons in Hndef.
    destruct Hndef as [H0 H1].
    apply stmt_eval_translate_eqns_cons in Heval.
    destruct Heval as [menv'' [env'' [Heval0 Heval1]]].
    apply IHeqs with (x:=x) (2:=H1) in Heval0.
    apply not_Is_variable_in_eq_stmt_eval_env_inv with (1:=H0) in Heval1.
    rewrite Heval1, Heval0.
    reflexivity.
Qed.

Local Ltac split_env_assumption :=
  match goal with
  | Henv: context Is_free_in_lexp [_], Hsem: sem_var_instant _ ?y _
    |- _ => apply Henv in Hsem; [destruct Hsem |solve [auto]]; clear Henv
  | Henv: context Is_free_in_clock [_], Hsem: sem_var_instant _ ?y _
    |- _ => apply Henv in Hsem; [destruct Hsem |solve [auto]]; clear Henv
  end.

Lemma clock_correct_true:
  forall R memories menv env ck,
    (forall x c, Is_free_in_clock x ck
                 -> sem_var_instant R x (present c)
                 -> (~PS.In x memories -> PM.find x env = Some c)
                    /\ (PS.In x memories -> mfind_mem x menv = Some c))
    -> sem_clock_instant R ck true
    -> Is_present_in memories menv env ck.
Proof.
  Hint Constructors Is_present_in.
  Hint Constructors sem_clock_instant.
  Hint Constructors Is_free_in_clock.
  Hint Constructors exp_eval.
  intros H memories menv env.
  induction ck as [|? ? x]; [ intuition | ].
  intro Henv.
  inversion_clear 1.
  constructor. apply IHck; auto.
  intros.
  split_env_assumption.
  apply exp_eval_tovar;
    destruct In_dec with x memories;
    intuition.
Qed.

Lemma get_exp_eval_tovar:
  forall x mems menv env v,
    (~ PS.In x mems -> PM.find x env = Some v)
    -> (PS.In x mems -> mfind_mem x menv = Some v)
    -> exp_eval menv env (tovar mems x) v.
Proof.
  intros x mems menv env v Hvar Hmem.
  unfold tovar.
  destruct (In_dec x mems) as [Hin|Hnin].
  - specialize (Hmem Hin).
    apply PS.mem_spec in Hin; rewrite Hin.
    constructor; exact Hmem.
  - specialize (Hvar Hnin).
    apply mem_spec_false in Hnin; rewrite Hnin.
    constructor; exact Hvar.
Qed.

Lemma clock_correct_false:
  forall R memories menv env ck,
    (forall x c, Is_free_in_clock x ck
                 -> sem_var_instant R x (present c)
                 -> (~PS.In x memories -> PM.find x env = Some c)
                    /\ (PS.In x memories -> mfind_mem x menv = Some c))
    -> sem_clock_instant R ck false
    -> Is_absent_in memories menv env ck.
Proof.
  Hint Constructors Is_absent_in sem_clock_instant Is_free_in_clock exp_eval.
  intros H memories menv env.
  induction ck as [|? ? x]; [ now inversion 2 | ].
  intro Henv.
  inversion_clear 1.
  constructor; apply IHck; now auto.
  apply clock_correct_true
  with (memories:=memories) (menv:=menv) (env:=env) in H1;
    [|now auto].
  apply IsAbs2 with (1:=H1) (3:=H3).
  split_env_assumption.
  destruct In_dec with x memories as [Hin|Hin];
    match goal with
    | H:~PS.In _ _ -> _, Hin:~PS.In _ _ |- _ => specialize (H Hin)
    | H:PS.In _ _ -> _, Hin:PS.In _ _ |- _ => specialize (H Hin)
    end;
    apply PS.mem_spec in Hin || apply mem_spec_false in Hin;
    unfold tovar;
    rewrite Hin;
    intuition.
Qed.

Lemma lexp_correct:
  forall R memories menv env c e,
    sem_lexp_instant R e (present c)
    -> (forall x c, Is_free_in_lexp x e
                    -> sem_var_instant R x (present c)
                    -> (~PS.In x memories -> PM.find x env = Some c)
                       /\ (PS.In x memories -> mfind_mem x menv = Some c))
    -> exp_eval menv env (translate_lexp memories e) c.
Proof.
  Hint Constructors exp_eval.
  intros R memories menv env c.
  induction e as [c0|y|e IH y yb];
    inversion 1; intros;
    try apply IH; try apply econst; auto.
  split_env_assumption;
    unfold translate_lexp;
    destruct (PS.mem y memories) eqn:Hm;
    rewrite PS.mem_spec in Hm || rewrite mem_spec_false in Hm;
    auto.
Qed.

Lemma laexp_correct:
  forall R memories menv env c e,
    sem_laexp_instant R e (present c)
    -> (forall x c, Is_free_in_laexp x e
                    -> sem_var_instant R x (present c)
                    -> (~PS.In x memories -> PM.find x env = Some c)
                       /\ (PS.In x memories -> mfind_mem x menv = Some c))
    -> exp_eval menv env (translate_laexp memories e) c.
Proof.
  intros H memories menv env c e Hsem Henv.
  destruct e as [ck ce].
  inversion Hsem as [? ? ? Hlexp|].
  change (exp_eval menv env (translate_lexp memories ce) c).
  eapply lexp_correct; try eassumption.
  intros; apply Henv.
  + constructor (assumption). 
  + assumption.
Qed.

Lemma cexp_correct:
  forall R memories prog menv env c x e,
    sem_cexp_instant R e (present c)
    -> (forall x c, Is_free_in_cexp x e
                    -> sem_var_instant R x (present c)
                    -> (~PS.In x memories -> PM.find x env = Some c)
                       /\ (PS.In x memories -> mfind_mem x menv = Some c))
    -> stmt_eval prog menv env (translate_cexp memories x e)
                                                        (menv, PM.add x c env).
Proof.
  intros R memories prog menv env c x.
  induction e as [b et IHt ef IHf|e].
  - (* Emerge *)
    inversion_clear 1; intro Henv.
    + apply Iifte_true.
      split_env_assumption.
      apply get_exp_eval_tovar; now auto.
      apply IHt; now auto.
    + apply Iifte_false.
      split_env_assumption.
      apply get_exp_eval_tovar; now auto.
      apply IHf; now auto.
  - (* Eexp *)
    inversion_clear 1; intro Henv.
    unfold translate_cexp.
    econstructor.
    eapply lexp_correct; [eassumption|now auto].
    reflexivity.
Qed.


Lemma caexp_correct:
  forall R memories prog menv env c x e,
    sem_caexp_instant R e (present c)
    -> (forall x c, Is_free_in_caexp x e
                    -> sem_var_instant R x (present c)
                    -> (~PS.In x memories -> PM.find x env = Some c)
                       /\ (PS.In x memories -> mfind_mem x menv = Some c))
    -> stmt_eval prog menv env (translate_caexp memories x e)
                                                  (menv, PM.add x c env).
Proof.
  intros H memories prog menv env c x e Hsem Henv.
  destruct e as [ck ce].
  inversion_clear Hsem as [? ? ? Hlexp ?|].
  change (stmt_eval prog menv env (translate_cexp memories x ce)
                     (menv, PM.add x c env)).
  apply cexp_correct with (1:=Hlexp); now auto.
Qed.

(* Notes:
   1. The assumption sem_equations must be shown for a set of equations.
      TODO: lemma showing that a well-typed and well-clocked set of
            equations has a semantics.

   2. The assumption stmt_eval (translate_eqns mems eqs) implies that an
      execution exists and thus that exp_eval's evar and estate find some
      value for each required variable.
      This is somehow backwards; it should be an obligation to show that
      an execution exists. This is something assured indirectly in the
      lemma below where we require not just that evar and estate find
      some value, but also that it is the correct value.
 *)


Lemma find_node_translate_find_class:
  forall f G node,
    find_node f G = Some node
    -> (exists prog',
           find_class f (translate G) = Some (translate_node node, prog')).
Proof.
  induction G as [|nd G IH]; [now inversion 1|].
  intros node Hfind.
  simpl; simpl in Hfind.
  destruct (ident_eqb nd.(n_name) f) eqn:Heq.
  - injection Hfind; intro He; rewrite He in *; clear He Hfind.
    exists (translate G); reflexivity.
  - apply IH in Hfind.
    destruct Hfind as [prog'].
    exists prog'.
    assumption.
Qed.

Lemma Is_memory_in_msem_var:
  forall G H M n x eqs c,
    Is_defined_in x eqs
    -> ~Is_variable_in x eqs
    -> sem_var_instant (restr H n) x (present c)
    -> List.Forall (msem_equation G H M) eqs
    -> (exists ms, mfind_mem x M = Some ms /\ ms n = c).
Proof.
  induction eqs as [|eq eqs IH];
  inversion_clear 1 as [? ? Hidi|? ? Hidi];
  intros Hnvi Hsv Hmsem;
  apply Forall_cons2 in Hmsem;
  apply not_Is_variable_in_cons in Hnvi;
  destruct Hnvi as [Hnvi0 Hnvi1];
  destruct Hmsem as [Hmeq Hmeqs].
  - destruct eq; inversion Hidi; subst;
    try (exfalso; apply Hnvi0; now constructor).
    inversion_clear Hmeq as [| |? ? ? ? ls ? ? ? Hmfind Hms0 Hsemls HxS Hmls].
    exists ms.
    split; [apply Hmfind|].
    specialize Hmls with n; specialize (HxS n); simpl in HxS.
    destruct (ls n);
      destruct Hmls as [Hms Hsv'].
    rewrite Hsv' in HxS.
    + assert (present c = absent) by sem_det; discriminate.
    + cut (present (ms n) = present c); [injection 1; auto|].
      rewrite Hsv' in HxS.
      sem_det.
  - apply IH; assumption.
Qed.

Local Ltac resolve_env_assumption :=
  let x := fresh in
  intro x; intros; split; intros;
  match goal with
  | Hws: Is_well_sch (PS.add ?input _) _ |- _ =>
    destruct (ident_eq_dec x input);
      repeat progress
             match goal with
             | H: exists _, _ |- _ => destruct H
             | H: _ /\ _ |- _ => destruct H

             | H:   ~PS.In x _,
               Hin: x=input,
               Hws: Is_well_sch _ (_ :: _) |- _ =>
                 subst;
                 apply Is_well_sch_free_variable_in_mems with input _ _ _ in Hws;
                 [| auto | apply PS.add_spec; auto ]

             | H1: x = input, H2: PS.In x ?mems |- _ =>
               rewrite H1 in H2; contradiction

             | H:   ~PS.In x _,
               Hin: x<>input,
               Hws: Is_well_sch _ (_ :: _) |- _ =>
                 apply Is_well_sch_free_variable with x _ _ _ in Hws;
                 [ | auto | intro HH; apply PS.add_spec in HH; intuition ]

             | H0:~Is_defined_in _ _,
               H1: stmt_eval _ _ _ _ _ |- _ =>
                 apply not_Is_defined_in_not_Is_variable_in in H0;
                 apply stmt_eval_translate_eqns_env_inv with (1:=H1) in H0;
                 rewrite H0

             | H:forall c:const, sem_var_instant _ _ _ <-> PM.find ?x ?env = _
                                 |- PM.find ?x ?env = _ => now (apply H)

             | Hv: forall x' : ident,
                 Is_variable_in x' ?eqs ->
                                forall c' : const,
                                  sem_var_instant ?R x' (present c')
                                  <-> PM.find x' ?env = Some c',
               Hs: sem_var_instant ?R ?x (present ?c)
               |-  PM.find ?x ?env = Some ?c => apply Hv; assumption

             | Hin: PS.In ?x ?mems,
               Hm: forall x, PS.In x ?mems -> _ /\ _
               |- mfind_mem ?x ?menv = Some _ =>
               specialize (Hm _ Hin)

             | H1: Is_defined_in ?x ?eqs,
               H2: ~Is_variable_in ?x ?eqs,
               H3: sem_var_instant ?R ?x (present ?c),
               H4: List.Forall (msem_equation ?G ?H ?M) ?eqs
               |- mfind_mem ?x ?menv = Some _ =>
               apply Is_memory_in_msem_var with (1:=H1) (2:=H2) (3:=H3) in H4

             | H1: ?ms ?n = ?c |- mfind_mem ?x ?menv = Some ?c => rewrite <- H1

             | Hin: PS.In ?x ?mems,
               Hwsch: Is_well_sch _ (_ :: _)
               |- mfind_mem ?x _ = Some _ =>
               inversion_clear Hwsch;
                 match goal with
                 | HH: forall i, ?isfreein i ?e -> ?Pvar /\ ?Pmem,
                   Hf: ?isfreein ?x ?e |- _  => apply HH in Hf; destruct Hf
                 end

             | H1: ?xs ?n = ?c,
               H2: mfind_mem ?x ?menv = Some (?xs ?n)
               |- mfind_mem ?x ?menv = Some ?c =>
               rewrite H1 in H2; exact H2

             | Hin: PS.In ?x ?mems,
               HH: PS.In ?x (PS.add _ ?mems) -> _ |- mfind_mem _ _ = Some _ =>
               eapply or_intror in Hin;
                 apply PS.add_spec in Hin;
                 specialize (HH Hin)

             | Hnd: ~Is_defined_in ?x ?eqs,
               Hstmt: stmt_eval _ _ _ (translate_eqns ?mems ?eqs) (?menv', _)
               |- mfind_mem ?x ?menv' = _ =>
               apply stmt_eval_translate_eqns_menv_inv with (2:=Hnd) in Hstmt;
                 rewrite Hstmt

             | Hidi: Is_defined_in ?x ?all,
               Hnvi: ~Is_variable_in ?x ?all,
               Hmc: Forall (Memory_Corres_eq _ ?n _ ?menv) ?all,
               Hmfind: mfind_mem ?x _ = Some ?vs
               |- mfind_mem ?x ?menv = Some (?vs ?n) =>
               now apply Is_memory_in_Memory_Corres_eqs
               with (1:=Hidi) (2:=Hnvi) (3:=Hmc) (4:=Hmfind)
             end
  end.

Lemma is_step_correct:
  forall (G: global)
         (H: history)
         (M: memory)
         (mems: PS.t)
         (alleqs : list equation),

    List.Forall (msem_equation G H M) alleqs

    -> forall (prog: program)
              (input: ident)
              (eqs: list equation)
              (n: nat)
              (menv: heap)
              (env: stack),
        (exists oeqs, alleqs = oeqs ++ eqs)
        -> (forall x, PS.In x mems
                      -> (Is_defined_in x alleqs
                          /\ ~Is_variable_in x alleqs))
        -> Welldef_global G
        -> prog = translate G

        (* - input (assumed) *)
        -> (forall c, sem_var_instant (restr H n) input (present c)
                      <-> PM.find input env = Some c)
          (* NB: PM.find x env' = Some c -> sem_var H x n (present c)
                 does not hold if PM.find x env = Some arbitrary_c, since
                 x will not be written to when its clock is absent.

                 It may just be better to show the direction:
                 sem_var H x n (present c) -> PM.find x env' = Some c

                 which is enough if the outputs are only sampled when
                 they are present (normally the case).

                 More discussion/context is needed. *)
        -> (forall x, Is_variable_in x eqs -> PM.find x env = None)
        -> ~PS.In input mems
        -> ~ Is_defined_in input eqs

        (* - execution of translated equations *)
        -> Is_well_sch (PS.add input mems) eqs

        (* - instantiated nodes (assumed) *)
        -> (forall f fnode xs M ys prog' fclass menv env,
               find_node f G = Some fnode
               -> msem_node G f xs M ys
               -> Memory_Corres G n f M menv
               -> find_class f prog = Some (fclass, prog')
               -> env = match xs n with
                        | absent => sempty
                        | present c => PM.add (c_input fclass) c sempty
                        end
               -> exists menv' env',
                   stmt_eval prog' menv env (c_step fclass) (menv', env')
                   /\ (forall c, ys n = present c
                                 <-> PM.find (c_output fclass) env' = Some c)
                   /\ Memory_Corres G (S n) f M menv')

        (* - unwritten memories (assumed) *)
        -> List.Forall (Memory_Corres_eq G n M menv) alleqs

        (* - locals (shown) *)
        -> (exists menv' env',
               stmt_eval prog menv env (translate_eqns mems eqs) (menv', env')
               /\ (forall x, Is_variable_in x eqs
                             -> forall c : const, sem_var_instant (restr H n) x (present c)
                                                  <-> PM.find x env' = Some c)
               (* - written memories (shown) *)
               /\ List.Forall (Memory_Corres_eq G (S n) M menv') eqs).
Proof.
  intros until alleqs.
  intros Hsems.

  (* Induction on equations: translate_eqns [] = Skip *)
  induction eqs as [|eq];
    [ intros; exists menv, env;
      split; [unfold translate_eqns; constructor|];
      split; intros; [ match goal with
                       | H:Is_variable_in _ nil |- _ => inversion H
                       end | now constructor ]| ].
  intros n menv env Hall Hinmems Hwdef Hprog Hin Henv Hin1 Hin2 Hwsch Hnode Hmc.

  assert (exists menv' env',
             stmt_eval prog menv env (translate_eqns mems eqs) (menv', env')
             /\ (forall x, Is_variable_in x eqs
                           -> forall c, sem_var_instant (restr H n) x (present c)
                                        <-> PM.find x env' = Some c)
             /\ List.Forall (Memory_Corres_eq G (S n) M menv') eqs) as IHeqs'.
  { eapply IHeqs.
    - apply List_shift_away with (1:=Hall).
    - exact Hinmems.
    - exact Hwdef.
    - exact Hprog.
    - exact Hin.
    - intros; apply Henv; constructor 2; auto.
    - exact Hin1.
    - apply not_Is_defined_in_cons with (1:=Hin2).
    - apply Is_well_sch_cons with (1:=Hwsch).
    - exact Hnode.
    - exact Hmc. }

  clear IHeqs.
  destruct IHeqs' as [menv' [env' [Hstmt [IHeqs0 IHeqs1]]]].

  destruct Hall as [oeqs Hall].
  assert (Hsems' := Hsems); rewrite Hall in Hsems'.

  apply Forall_app in Hsems'.
  destruct Hsems' as [H0 Hsems']; clear H0.
  apply Forall_cons2 in Hsems'.
  destruct Hsems' as [Hsem Hsems'].

  inversion Hsem as [H0 M0 x xs cae Hvar Hcae HR1 HR2 HR3
                    |H0 M0 y f Mo lae ls xs Hmfind Hlae Hvar Hmsem HR1 HR2 HR3
                    |H0 M0 ms y ls yS v0 lae Hmfind Hms0 Hlae HyS Hvar HR1 HR2 HR3];
    (rewrite <-HR3 in *; clear HR1 HR2 HR3 H0 M0);
    specialize (Hvar n).
  - (* Case EqDef: y = cae *)
    exists menv'. (* the memory does not change *)
    specialize (Hcae n); simpl in *.
    assert (forall x c,
               Is_free_in_caexp x cae
               -> sem_var_instant (restr H n) x (present c)
               -> (~ PS.In x mems -> PM.find x env' = Some c)
                  /\ (PS.In x mems -> mfind_mem x menv' = Some c))
      as Hcae' by resolve_env_assumption.
    destruct (xs n).
    + (* xs n = absent *)
      exists env'.
      inversion Hcae as [|? ? Hcexp Hclk HR1 HR2];
        rewrite <-HR1 in *; clear HR1 HR2 .
      split; [|split].
      * apply stmt_eval_translate_eqns_cons.
        exists menv', env'.
        split; [exact Hstmt|].
        apply stmt_eval_Control_absent.
        apply clock_correct_false with (2:=Hclk); now auto.
      * intros x0 Hivi c.
        (* TODO: do we really need this [destruct]? It seems that we *know* that it cannot be a variable (proof by [contradiction]/[discriminate]).
                 If not, remove dependency on [Dataflow.IsVariable.Decide] *)
        destruct (Is_variable_in_dec x0 eqs) as [Hvin|Hvin];
          [now apply IHeqs0 with (1:=Hvin)|].
        apply stmt_eval_translate_eqns_env_inv with (2:=Hvin) in Hstmt.
        rewrite Hstmt.
        inversion_clear Hivi as [? ? Hivi'|];
          [|unfold Is_variable_in in Hvin; contradiction].
        inversion_clear Hivi'.
        split; intro Hsv'.
        assert (present c = absent) by sem_det. discriminate.
        assert (PM.find x env = None).
        apply Henv; now repeat constructor.
        rewrite Hsv' in *; discriminate.
      * rewrite Hall in Hmc.
        apply Forall_app in Hmc; destruct Hmc as [Hmc0 Hmc]; clear Hmc0.
        apply Forall_cons2 in Hmc; destruct Hmc as [Hmc Hmc0]; clear Hmc0.
        inversion_clear Hmc.
        repeat constructor; assumption.
    + (* xs n = present *)
      exists (PM.add x v env').
      inversion Hcae as [? ? ? Hcexp Hclk HR1 HR2|];
        rewrite <-HR1 in *; clear HR1 HR2 c.
      split; [|split].
      * apply stmt_eval_translate_eqns_cons.
        exists menv', env'.
        split; [exact Hstmt|].
        apply stmt_eval_Control_present.
        apply clock_correct_true with (2:=Hclk); now auto.
        apply cexp_correct with (1:=Hcexp); now auto.
      * intros x0 Hivi c.
        inversion_clear Hivi as [? ? Hivi'|]; [inversion_clear Hivi'|].
        { rewrite PM.gss; split; intro HH.
          - assert (c = v) by
              (cut (present c = present v); [injection 1; auto|];
               sem_det).
            congruence.
          - injection HH; intro Heq. subst. assumption. }
        { destruct (ident_eq_dec x0 x) as [Hxy|Hnxy].
          - rewrite Hxy in *; clear Hxy.
            rewrite PM.gss.
            split; intro Hsv'.
            * assert (v = c) 
                by (cut (present v = present c); [injection 1; auto|]; sem_det).
              congruence.
            * injection Hsv'; congruence.
          - erewrite PM.gso; try eassumption.
            apply IHeqs0; assumption.  
        }
      * rewrite Hall in Hmc.
        apply Forall_app in Hmc; destruct Hmc as [Hmc0 Hmc]; clear Hmc0.
        apply Forall_cons2 in Hmc; destruct Hmc as [Hmc Hmc0]; clear Hmc0.
        inversion_clear Hmc.
        repeat constructor; assumption.
  - (* Case EqApp: y = f lae *)
    specialize (Hlae n).
    (* used variables are defined *)
    assert (forall x c,
               Is_free_in_laexp x lae
               -> sem_var_instant (restr H n) x (present c)
               -> (~ PS.In x mems -> PM.find x env' = Some c)
                  /\ (PS.In x mems -> mfind_mem x menv' = Some c))
      as Hlae' by resolve_env_assumption.
    (* memory correspondence before execution *)
    rewrite Hall in Hmc.
    apply Forall_app in Hmc.
    destruct Hmc as [Hmc0 Hmc]; clear Hmc0.
    apply Forall_cons2 in Hmc.
    destruct Hmc as [Hmceq Hmceqs].
    inversion_clear Hmceq as [|? ? ? ? ? Hmc0|].
    specialize (Hmc0 _ Hmfind).
    destruct Hmc0 as [omenv [Hfindo Hmc0]].
    (* dataflow semantics *)
    assert (Hmsem':=Hmsem).
    inversion_clear Hmsem' as [? ? ? ? i o neqs Hfind Hnsem].
    destruct Hnsem as [Hn [Hlsn [Hxsn [Habs [Hout Hnsem]]]]].
    specialize (Hlsn n);
      specialize (Hxsn n);
      specialize (Habs n);
      specialize (Hout n); 
      simpl in *.
    (* no other instance *)
    assert (~Is_defined_in y eqs) as Hniii
        by (inversion_clear Hwsch; assumption).
    destruct (ls n) eqn:Hls.
    + (* y = absent *)
      exists menv', env'.
      specialize (Habs (eq_refl absent)).
      assert (xs n = absent) as Hout'
          by (apply Hout; reflexivity); clear Hout.
      inversion Hlae as [|? ? Hlexp Hclk HR1 HR2];
        rewrite <-HR1 in *; clear HR1 HR2.
      split; [|split].
      * apply stmt_eval_translate_eqns_cons.
        exists menv', env'.
        split; [exact Hstmt|].
        apply stmt_eval_Control_absent.
        apply clock_correct_false with (2:=Hclk); now auto.
      * intros x Hivi c.
        destruct (Is_variable_in_dec x eqs) as [Hvin|Hvin];
          [now apply IHeqs0 with (1:=Hvin)|].
        apply stmt_eval_translate_eqns_env_inv with (2:=Hvin) in Hstmt.
        rewrite Hstmt.
        inversion_clear Hivi as [? ? Hivi'|];
          [|unfold Is_variable_in in Hvin; contradiction].
        inversion Hivi' as [|x' f' e HR1 [HR2 HR3 HR4]];
          rewrite HR2 in *; clear HR1 HR2 HR3 HR4 x' f' e Hivi'.
        split; intro Hsv'.
        { inversion_clear Hsv' as [Hfind'].
          inversion_clear Hvar as [Hfind''].
          rewrite Hfind' in Hfind''.
          injection Hfind''; intro HR1; rewrite <-HR1 in *; clear HR1 Hfind''.
          discriminate. }
        { assert (PM.find y env = None) as Hnone
              by (apply Henv; repeat constructor).
          rewrite Hnone in Hsv'.
          discriminate. }
      * apply Forall_cons; [|now apply IHeqs1].
        constructor.
        intros Mo' Hmfind'.
        rewrite Hmfind in Hmfind'.
        injection Hmfind'; intro He; rewrite <-He in *; clear He Hmfind'.
        exists omenv.
        rewrite stmt_eval_translate_eqns_minst_inv with (1:=Hstmt) (2:=Hniii).
        split; [exact Hfindo|].
        now apply Memory_Corres_unchanged
        with (1:=Hwdef) (2:=Hmsem) (3:=Hls) (4:=Hmc0).
    + (* y = present *)
      pose proof (find_node_translate_find_class _ _ _ Hfind) as Hfindc.
      destruct Hfindc as [prog' Hfindc].
      assert (
          let fclass := translate_node (mk_node f i o neqs) in
          exists omenv' oenv',
            stmt_eval prog' omenv (PM.add i v sempty)
                      (c_step fclass) (omenv', oenv')
            /\ (forall c,
                   xs n = present c
                   <-> PM.find (c_output fclass) oenv' = Some c)
            /\ Memory_Corres G (S n) f Mo omenv') as Hnode'.
      { eapply Hnode with (1:=Hfind) (2:=Hmsem) (3:=Hmc0).
        - rewrite Hprog; exact Hfindc.
        - rewrite Hls; reflexivity. }
      clear Hnode.
      simpl in *.
      destruct Hnode' as [omenv' [oenv' [Hnstmt [Hnout Hnmc]]]].
      assert (xs n <> absent) as Hxsp
          by (intro HH; apply Hout in HH; discriminate).
      apply not_absent_present in Hxsp.
      destruct Hxsp as [c Hxsc].
      exists (madd_obj y omenv' menv'), (PM.add y c env').
      inversion Hlae as [? ? c0 Hlexp Hclk HR1 HR2|];
        rewrite <-HR1 in *; clear HR1 HR2 c0.
      split; [|split].
      * apply stmt_eval_translate_eqns_cons.
        exists menv', env'.
        split; [exact Hstmt|].
        apply stmt_eval_Control_present.
        apply clock_correct_true with (2:=Hclk); now auto.
        apply lexp_correct
        with (memories:=mems) (menv:=menv') (env:=env') in Hlexp;
          [|now auto].
        rewrite Hprog in *.
        rewrite <-stmt_eval_translate_eqns_minst_inv
        with (1:=Hstmt) (2:=Hniii) in Hfindo.
        eapply Istep with (1:=Hfindo) (2:=Hlexp);
          [|reflexivity|reflexivity].
        eapply Iestep with (1:=Hfindc) (2:=Hnstmt).
        now apply Hnout with (1:=Hxsc).
      * intros x Hivi c'.
        inversion_clear Hivi as [? ? Hivi'|]; [inversion_clear Hivi'|].
        { rewrite PM.gss; split; intro HH.
          - assert ((xs n) = present c') by sem_det.
            congruence.
          - injection HH; intro Heq; rewrite <-Heq.
            rewrite <-Hxsc; exact Hvar. }
        { destruct (ident_eq_dec x y) as [Hxy|Hnxy].
          - rewrite Hxy in *; clear Hxy.
            rewrite PM.gss.
            split.
            * intro Hvar'.
              assert (present c' = xs n) by sem_det.
              congruence.
            * intro Hsv'.
              injection Hsv'; intro Heq; rewrite <-Heq in *.
              rewrite Hxsc in Hvar; exact Hvar.
          - rewrite (PM.gso _ _ Hnxy).
            apply IHeqs0; assumption. }
      * apply Forall_cons.
        2:now apply Memory_Corres_eqs_add_obj with (1:=IHeqs1) (2:=Hniii).
        constructor.
        intros Mo' Hmfind'.
        rewrite Hmfind in Hmfind'.
        injection Hmfind'; intro Heq; rewrite <-Heq in *; clear Heq Hmfind'.
        exists omenv'.
        split; [rewrite mfind_inst_gss; reflexivity|exact Hnmc].
  - (* Case EqFby: y = v0 fby lae *)
    specialize (Hlae n).
    assert (forall x c,
               Is_free_in_laexp x lae
               -> sem_var_instant (restr H n) x (present c)
               -> (~ PS.In x mems -> PM.find x env' = Some c)
                  /\ (PS.In x mems -> mfind_mem x menv' = Some c))
      as Hlae' by resolve_env_assumption.
    destruct (ls n) eqn:Hls;
      destruct Hvar as [Hms Hvar].
    + (* y = absent *)
      exists menv', env'.
      inversion Hlae as [|? ? Hlexp Hclk HR1 HR2];
        rewrite <-HR1 in *; clear HR1 HR2.
      split; [|split].
      * apply stmt_eval_translate_eqns_cons.
        exists menv', env'.
        split; [exact Hstmt|].
        apply stmt_eval_Control_absent.
        apply clock_correct_false with (2:=Hclk); now auto.
      * intros x Hivi c.
        destruct (Is_variable_in_dec x eqs) as [Hvin|Hvin];
          [now apply IHeqs0 with (1:=Hvin)|].
        apply stmt_eval_translate_eqns_env_inv with (2:=Hvin) in Hstmt.
        rewrite Hstmt.
        inversion_clear Hivi as [? ? Hivi'|];
          [|unfold Is_variable_in in Hvin; contradiction].
        inversion_clear Hivi'.
      * constructor.
        2:assumption.
        constructor.
        intros ms0' Hmfind'.
        rewrite Hmfind in Hmfind'.
        injection Hmfind'; intro Heq; rewrite <-Heq in *; clear Heq Hmfind'.
        (* TODO: do we really need this? We seem to *know* that it
        cannot be equal ([exfalso] branch).

                 If unnecessary, remove the import on Dataflow.IsDefined.Decide *)
        destruct (Is_defined_in_dec y eqs) as [Hxin|Hxin];
          [ exfalso; inversion_clear Hwsch; now intuition|].
        rewrite Hall in Hmc.
        apply Forall_app in Hmc.
        destruct Hmc as [H0 Hmc]; clear H0.
        apply Forall_cons2 in Hmc.
        destruct Hmc as [Hmc H0]; clear H0.
        inversion_clear Hmc as [| |? ? ? ? ? Hfindc].
        rewrite Hms.
        apply stmt_eval_translate_eqns_menv_inv with (2:=Hxin) in Hstmt.
        rewrite Hstmt.
        apply Hfindc with (1:=Hmfind).
    + (* y = present *)
      exists (madd_mem y v menv'), env'.
      inversion Hlae as [? ? v' Hlexp Hclk HR1 HR2|];
        rewrite <-HR1 in *; clear HR1 HR2 v'.
      split; [|split].
      * apply stmt_eval_translate_eqns_cons.
        exists menv', env'.
        split; [exact Hstmt|].
        apply stmt_eval_Control_present.
        apply clock_correct_true with (2:=Hclk); now auto.
        econstructor.
        apply lexp_correct with (1:=Hlexp); now auto.
        reflexivity.
      * intros x Hivi c.
        inversion_clear Hivi as [? ? Hivi'|]; [now inversion_clear Hivi'|].
        apply IHeqs0.
        assumption.
      * rewrite <-Hms.
        apply Forall_cons.
        2:now apply Memory_Corres_eqs_add_mem with (1:=Hmfind) (2:=IHeqs1).
        constructor.
        intros ms' Hmfind'.
        rewrite Hmfind in Hmfind'.
        injection Hmfind'; intro Heq; rewrite <-Heq in *; clear Hmfind' Heq.
        now apply mfind_mem_gss.
Qed.

(* TODO: rework using (a simplified version of) msem_node_mult *)
Lemma is_node_correct:
  forall (G: global)
         (f: ident)
         (fnode: node)
         (xs: stream value)
         (M: memory)
         (ys: stream value)

         (prog': program)
         (fclass: class)
         (n: nat)
         (menv: heap)
         (env: stack),

    Welldef_global G
    -> find_node f G = Some fnode
    -> msem_node G f xs M ys
    -> Memory_Corres G n f M menv

    -> find_class f (translate G) = Some(fclass, prog')
    -> env = (match xs n with
	      | present c => PM.add fclass.(c_input) c sempty
	      | absent => sempty
             end)

    -> (exists menv' env',
           stmt_eval prog' menv env fclass.(c_step) (menv', env')
           /\ (forall c, ys n =present c
                         <-> PM.find (fclass.(c_output)) env' = Some c)
           /\ Memory_Corres G (S n) f M menv').
Proof.
  (* TODO: Develop a version of msem_node_mult that works for eqs? *)
  induction G as [|node G IH]; [now inversion 4|].
  intros f fnode xs M ys prog' fclass n menv env.
  intros Hwd Hfind Hmsem Hmc Hfcls Henv.
  assert (Ordered_nodes (node::G)) as Hord
      by apply Welldef_global_Ordered_nodes with (1:=Hwd).

  simpl in Hfind,Hfcls.
  destruct (ident_eqb (n_name node) f) eqn:Hfeq.

  Focus 2.
  { apply Welldef_global_cons in Hwd.
    assert (node.(n_name) <> f) as Hfneq
        by apply Pos.eqb_neq with (1:=Hfeq).
    rewrite Memory_Corres_node_tl with (1:=Hord) (2:=Hfneq) in Hmc.
    apply msem_node_cons with (1:=Hord) (3:=Hfneq) in Hmsem.
    apply IH with (1:=Hwd) (2:=Hfind) (3:=Hmsem) (4:=Hmc) (6:=Henv) (ys:=ys)
      in Hfcls.
    destruct Hfcls as [menv' [env' [Hstmt [Hout Hmc']]]].
    exists menv',env'.
    rewrite Memory_Corres_node_tl with (1:=Hord) (2:=Hfneq).
    intuition. }
  Unfocus.

  injection Hfcls; intros HR1 HR2; rewrite <-HR1,<-HR2 in *; clear HR1 HR2 Hfcls.
  injection Hfind; intro HR1; rewrite HR1 in *; clear HR1 Hfind.

  inversion_clear Hmsem as [? ? ? ? i o eqs Hfind Hsem].
  simpl in Hfind.
  rewrite Hfeq in Hfind; clear Hfeq.
  injection Hfind; intro HR1; rewrite HR1 in *; clear HR1 Hfind.
  simpl in *.

  destruct Hsem as [H [Hi [Ho [Hclk [Hsout Hsem]]]]].
  specialize (Hi n).
  specialize (Ho n).

  inversion_clear Hwd as [|? ? Hwd' neqs ni no Hwsch Hin2 Hout Hnode Hnd Hfind'].
  rename Hwd' into Hwd.
  simpl in *.
  unfold neqs, ni, no in *; clear neqs ni no.

  assert (
      exists menv' env',
        stmt_eval (translate G) menv env
                  (translate_eqns (memories eqs) eqs) (menv', env')
        /\ (forall x, Is_variable_in x eqs ->
                     forall c, sem_var_instant (restr H n) x (present c)
                               <-> PM.find x env' = Some c)
        /\ Forall (Memory_Corres_eq G (S n) M menv') eqs) as His_step_correct.
  {
    eapply is_step_correct with (mems:=memories eqs) (env:=env) (menv:=menv)
                                (input:=i) (prog:=translate G).
    (* TODO: Tidy this up... *)
    - apply Forall_msem_equation_global_tl
      with (1:=Hord) (2:=Hnd) (3:=Hnode) (4:=Hsem).
    - exists []; trivial.
    - intros y Hinm.
      assert (NoDup_defs eqs) as Hndds
          by apply Is_well_sch_NoDup_defs with (1:=Hwsch).
      split; [now apply Is_defined_in_memories with (1:=Hinm)
             |now apply not_Is_variable_in_memories with (1:=Hinm) (2:=Hndds)].
    - exact Hwd.
    - reflexivity.
    - intro c.
      rewrite Henv.
      split; intro HH.
      assert (Hxs_pres: xs n = present c) by sem_det.
      rewrite Hxs_pres in *.
      apply PM.gss.
      destruct (xs n) as [|v Hxs];
        [ rewrite PM.gempty in HH; discriminate |].
      rewrite PM.gss in HH; injection HH; intro HR1;
      rewrite HR1 in *; exact Hi.
    - rewrite Henv.
      intros x Hivi.
      destruct (xs n); [rewrite PM.gempty;reflexivity|].
      rewrite PM.gso; [rewrite PM.gempty;reflexivity|].
      apply Is_variable_in_Is_defined_in in Hivi.
      intro Hx; rewrite Hx in *.
      contradiction.
    - intro Hin.
      apply Is_defined_in_memories in Hin.
      contradiction.
    - exact Hin2.
    - exact Hwsch.
    - apply (fun f fnode xs M ys prog' fclass menv env Hfind Hmsem
             => IH f fnode xs M ys prog' fclass n menv env Hwd Hfind Hmsem).
    - inversion_clear Hmc as [? ? ? ? ? ? Hf Hmeqs].
      simpl in Hf.
      rewrite ident_eqb_refl in Hf.
      injection Hf; intros Heq0 Heq1 Heq2;
      rewrite <-Heq0, <-Heq1, <-Heq2 in *;
      clear Heq0 Heq1 Heq2 Hf.
      apply Memory_Corres_eqs_node_tl with (1:=Hord) (2:=Hnode) (3:=Hmeqs).
  }
  clear IH.
  destruct His_step_correct as [menv' [env' [Hstmt [Hvar' Hmc']]]].
  exists menv',env'.
  split; [rewrite ps_from_list_gather_eqs_memories; exact Hstmt|].
  split.
  - intro c.
    apply Hvar' with (c:=c) in Hout.
    rewrite <- Hout.
    split; intro HH;
      [ rewrite HH in Ho; exact Ho 
      | sem_det ].
  - econstructor.
    + simpl; rewrite Pos.eqb_refl; reflexivity.
    + apply Memory_Corres_eqs_node_tl with (1:=Hord) (2:=Hnode) (3:=Hmc').
Qed.

Lemma stmt_eval_translate_reset_eqn_shift:
  forall prog eqs iacc menv env menv' env',
    stmt_eval prog menv env
              (List.fold_left translate_reset_eqn eqs iacc)
              (menv', env')
    <->
    exists menv'' env'',
      stmt_eval prog menv env
                (List.fold_left translate_reset_eqn eqs Skip)
                (menv'', env'')
      /\
      stmt_eval prog menv'' env'' iacc (menv', env').
Proof.
  Hint Constructors stmt_eval.
  induction eqs as [|eq eqs IH].
  - split; [ now eauto | ].
    intro H; do 2 destruct H.
    destruct H as [H0 H1].
    inversion_clear H0; apply H1.
  - intros.
    split.
    + intro H0.
      apply IH in H0.
      destruct H0 as [menv'' [env'' [H0 H1]]].
      destruct eq; [now eauto| |];
      inversion_clear H1;
      exists menv1; exists env1;
      split; try (simpl; apply IH); eauto.
    + intros.
      destruct eq; [ now (apply IH; auto) | |];
      (apply IH;
       simpl in H;
       destruct H as [menv'' [env'' [H0 H1]]];
       apply IH in H0;
       destruct H0 as [menv0 [env0 [H2 H3]]];
       exists menv0; exists env0;
       split; [now auto|];
       inversion_clear H3;
       inversion H0; subst;
       econstructor; eauto).
Qed.

Lemma stmt_eval_translate_reset_eqns_cons:
  forall prog menv env (eq:equation) eqs P,
    (exists menv'' env'',
        stmt_eval prog menv env
                  (translate_reset_eqns (eq :: eqs)) (menv'', env'')
        /\ P menv'' env'')
    <->
    (exists menv' env' menv'' env'',
        stmt_eval prog menv env (translate_reset_eqns eqs) (menv', env')
        /\ stmt_eval prog menv' env'
                     (translate_reset_eqn Skip eq) (menv'', env'')
        /\ P menv'' env'').
Proof.
  split.
  - intro H.
    destruct H as [menv'' [env'' H]].
    unfold translate_reset_eqns in H.
    simpl in H.
    destruct H as [H HP].
    apply stmt_eval_translate_reset_eqn_shift in H.
    destruct H as [menv' [env' [H1 H2]]].
    exists menv', env', menv'', env''.
    now intuition.
  - intro H.
    destruct H as [menv' [env' [menv'' [env'' [H1 [H2 HP]]]]]].
    unfold translate_reset_eqns.
    simpl.
    exists menv'', env''.
    intuition.
    apply stmt_eval_translate_reset_eqn_shift.
    exists menv', env'.
    intuition.
Qed.

(* TODO: rework using (a simplified version of) msem_node_mult *)
(* This lemma is stated with Welldef_global rather than simply Ordered_nodes
   since the former gives Is_well_sch for each set of node equations within
   the global environment. In turn, Is_well_sch guarantees that any EqApp
   equation is unique, i.e., given x = EqApp f e, there is no other instance
   of x. This greatly simplifies the induction where it must be shown from

   that

   The case of EqFby is simpler due to the lemma Memory_Corres_eqs_add_mem.
   The comment following this lemma explains why there can be no corresponding
   Memory_Corres_eqs_add_obj lemma. *)
Lemma is_node_reset_correct:
  forall (G: global)
         (f: ident)
         (xs: stream value)
         (M: memory)
         (ys: stream value)
         (fnode: node)

         (prog': program)
         (fclass: class)
         (menv: heap)
         (env: stack),

       Welldef_global G
    -> msem_node G f xs M ys
    -> find_node f G = Some fnode

    -> find_class f (translate G) = Some(fclass, prog')
    -> (exists menv' env',
           stmt_eval prog' hempty env fclass.(c_reset) (menv', env')
           /\ Memory_Corres G 0 f M menv').
Proof.
  induction G as [|node G IH]; [now inversion 1|].
  intros until env.
  intros Hwdef Hmsem Hfindn Hfindc.

  assert (Hfindn':=Hfindn).
  pose proof (Welldef_global_Ordered_nodes _ Hwdef) as Hord.
  pose proof (find_node_not_Is_node_in _ _ _ Hord Hfindn) as Hnni.
  pose proof (find_node_translate_find_class _ _ _ Hfindn) as HH.
  destruct HH as [prog'' Hfindc'].
  rewrite Hfindc in Hfindc'.
  injection Hfindc';
    intros HR1 HR2; rewrite <-HR1,HR2 in *; clear HR1 HR2 Hfindc' prog''.
  simpl in Hfindc, Hfindn'.
  destruct (ident_eqb (n_name node) f) eqn:Heqb;
    [ apply Pos.eqb_eq in Heqb | apply Pos.eqb_neq in Heqb ].
  - injection Hfindn'; intro HR1; rewrite <-HR1 in *; clear HR1 Hfindn'.
    injection Hfindc; intros HR1; rewrite <-HR1 in *; clear HR1 Hfindc.
    destruct node as [f' i o eqs].
    simpl in Heqb.
    rewrite Heqb in *; clear Heqb.
    cut (exists menv' env',
            stmt_eval (translate G) hempty env (translate_reset_eqns eqs)
                      (menv', env')
            /\ Forall (Memory_Corres_eq G 0 M menv') eqs).
    { destruct 1 as [menv' [env' [Hstmt Hmc]]].
      exists menv', env'.
      split; [exact Hstmt|].
      econstructor; [exact Hfindn|].
      apply Memory_Corres_eqs_node_tl with (1:=Hord) (2:=Hnni).
      exact Hmc. }
    simpl in *.
    inversion_clear Hmsem as [? ? ? ? i' o' eqs' Hfind HH].
    simpl in Hfind; rewrite ident_eqb_refl in Hfind.
    injection Hfind; intros HR1 HR2 HR3; rewrite <-HR1,<-HR2,<-HR3 in *;
    clear HR1 HR2 HR3 Hfind f' i' o' eqs'.
    destruct HH as [H [Hxs [Hys [Habs [Hout Hmsem]]]]].
    apply Forall_msem_equation_global_tl with (1:=Hord) (3:=Hnni) in Hmsem.
    2:(intros f' Hini'; apply find_node_Exists; inversion_clear Hord;
       apply H1 in Hini'; now intuition).
    clear Hxs Hys Habs Hout Hfindn Hord.
    assert (exists mems, Is_well_sch mems eqs) as Hwsch
        by (inversion_clear Hwdef; exists (PS.add ni (memories eqs0)); intuition).
    destruct Hwsch as [mems Hwsch].
    apply Welldef_global_cons in Hwdef.
    induction eqs as [|eq eqs IHeqs]; [exists hempty, env; now intuition|].
    apply not_Is_node_in_cons in Hnni.
    destruct Hnni as [Hnni Hnnis].
    apply Forall_cons2 in Hmsem; destruct Hmsem as [Hmsem Hmsems].
    specialize (IHeqs Hnnis Hmsems (Is_well_sch_cons _ _ _ Hwsch)); clear Hnnis.
    destruct IHeqs as [menv' [env' [Hstmt Hmc]]].
    rewrite stmt_eval_translate_reset_eqns_cons.
    exists menv', env'.
    destruct eq as [|y g lae|y v0 lae].
    + (* EqDef *)
      exists menv', env'.
      split; [exact Hstmt|].
      split; [constructor|].
      inversion_clear Hwsch.
      repeat constructor.
      exact Hmc.
    + (* EqApp *)
      inversion_clear Hmsem as [|? ? ? ? Mo ? xs' ys' Hmfind Hxs' Hys' Hmsem'|].
      assert (Hmsem'':=Hmsem').
      inversion_clear Hmsem'' as [? ? ? ? ? ? ? Hfindn HH].
      pose proof (find_node_translate_find_class _ _ _ Hfindn) as Hfindc.
      destruct Hfindc as [prog'' Hfindc].
      specialize (IH _ _ _ _ _ _ _ hempty sempty Hwdef Hmsem' Hfindn Hfindc).
      destruct IH as [omenv [oenv [Hstmtn Hmcn]]].
      exists (madd_obj y omenv menv'), env'.
      split; [exact Hstmt|].
      split.
      * econstructor; [|now econstructor].
        econstructor; [|reflexivity].
        econstructor; [exact Hfindc|].
        exact Hstmtn.
      * inversion_clear Hwsch.
        repeat constructor;
          [|apply Memory_Corres_eqs_add_obj with (1:=Hmc); assumption].
        intros M' Hmfind'.
        rewrite Hmfind in Hmfind'.
        injection Hmfind'; intro HR; rewrite <-HR; clear HR Hmfind' M'.
        exists omenv.
        split; [now apply mfind_inst_gss|now intuition].
    + (* EqFby *)
      exists (madd_mem y v0 menv'), env'.
      split; [exact Hstmt|].
      split.
      * econstructor; [|now econstructor].
        econstructor; [|reflexivity].
        now constructor.
      * inversion_clear Hmsem as [| |? ? ? ? ? ? ? ? Hmfind Hms Hlae Hls].
        rewrite <-Hms in *.
        repeat constructor;
          [| now apply Memory_Corres_eqs_add_mem with (1:=Hmfind) (2:=Hmc)].
        intros ms' Hmfind'.
        rewrite Hmfind in Hmfind'.
        injection Hmfind'; intro HR; rewrite HR in *; clear HR Hmfind'.
        rewrite mfind_mem_gss.
        reflexivity.
  - apply Welldef_global_cons in Hwdef.
    apply msem_node_cons with (1:=Hord) (3:=Heqb) in Hmsem.
    specialize (IH _ _ _ _ _ _ _ menv env Hwdef Hmsem Hfindn' Hfindc).
    destruct IH as [menv' [env' [Hstmt Hmc]]].
    exists menv', env'.
    split; [exact Hstmt|].
    now apply Memory_Corres_node_tl with (1:=Hord) (2:=Heqb) (3:=Hmc).
Qed.

Lemma is_translate_correct:
  forall (G: global)
         (f: ident)
         (xs: stream value)
         (ys: stream value)
         (ci: const)
         (r: ident)
         (obj: ident)
         (n: nat),
    Welldef_global G
    -> sem_node G f xs ys
    -> (forall n, xs n = present ci)
    -> (exists menv env,
           stmt_eval (translate G) hempty sempty
             (Comp (Reset_ap f obj)
                   (Repeat (S n) (Step_ap r f obj (Const ci)))) (menv, env)
           /\ (forall co, ys n = present co <-> PM.find r env = Some co)).
Proof.
  (* TODO: Ugly, dumb proof. How to streamline / beautify all these
           inversions and rewrites. *)
  intros until n.
  intros Hwdef Hsem Hxs.
  apply sem_msem_node with (1:=Hwdef) in Hsem.
  destruct Hsem as [M Hmsem].
  assert(Hmsem':=Hmsem).
  inversion_clear Hmsem' as [? ? ? ? ? ? ? Hfindn Hmsem''].
  destruct Hmsem'' as [H [Hi [Ho [Hclk [Habs Hmsem']]]]].
  pose proof (find_node_translate_find_class _ _ _ Hfindn) as Hfindc.
  destruct Hfindc as [prog' Hfindc].
  pose proof (is_node_reset_correct _ _ _ _ _ _ _ _
                                    hempty sempty Hwdef Hmsem Hfindn Hfindc) as Hreset.
  destruct Hreset as [menv' [env' [Hstmtr Hmc0]]].
  simpl in Hstmtr.
  cut (exists menv env,
          stmt_eval (translate G) hempty sempty
                    (Comp (Reset_ap f obj) (Repeat (S n)
                                               (Step_ap r f obj (Const ci))))
                    (menv, env)
          /\ (forall co, ys n = present co <-> PM.find r env = Some co)
          /\ (exists omenv, mfind_inst obj menv = Some omenv
                            /\ Memory_Corres G (S n) f M omenv)).
  destruct 1 as [menv [env [Hstmt [Hout Hmc]]]]; exists menv, env; now intuition.
  induction n.
  - specialize Hxs with 0%nat.
    pose proof (is_node_correct _ _ _ _ _ _ _ _ _ _ (PM.add i ci sempty)
                                Hwdef Hfindn Hmsem Hmc0 Hfindc) as Hstmt.
    rewrite Hxs in Hstmt; specialize (Hstmt (eq_refl _)); simpl in Hstmt.
    destruct Hstmt as [menv'' [env'' [Hstmt [Hout Hmc]]]].
    assert (ys 0%nat <> absent) as Hyna
        by (intro HH; apply Habs in HH; rewrite Hxs in HH; discriminate).
    apply not_absent_present in Hyna.
    destruct Hyna as [yc Hyna].
    exists (madd_obj obj menv'' (madd_obj obj menv' hempty)).
    exists (PM.add r yc sempty).
    split; [|split].
    + (* TODO: automate! *)
      econstructor.
      econstructor; [|reflexivity].
      econstructor; [exact Hfindc|exact Hstmtr].
      econstructor.
      econstructor.
      econstructor.
      apply mfind_inst_gss.
      econstructor.
      econstructor.
      exact Hfindc.
      apply Hstmt.
      apply Hout.
      apply Hyna.
      reflexivity.
      reflexivity.
    + (* TODO: automate *)
      intuition.
      rewrite H0 in Hyna.
      injection Hyna; intro; subst.
      apply PM.gss.
      rewrite PM.gss in H0.
      injection H0; intro; subst.
      exact Hyna.
    + exists menv''.
      intuition.
      apply mfind_inst_gss.
  - destruct IHn as [menv'' [env'' [Hstmts [H0 Hmc]]]]; clear H0.
    destruct Hmc as [omenv [Hfindo Hmc]].
    inversion_clear Hstmts.
    specialize Hxs with (S n).
    pose proof (is_node_correct _ _ _ _ _ _ _ _ _ _ (PM.add i ci sempty)
                                Hwdef Hfindn Hmsem Hmc Hfindc) as Hstmt.
    rewrite Hxs in Hstmt; specialize (Hstmt (eq_refl _)); simpl in Hstmt.
    destruct Hstmt as [menv''' [env''' [Hstmt [Hout Hmc']]]].
    assert (ys (S n) <> absent) as Hyna
        by (intro HH; apply Habs in HH; rewrite Hxs in HH; discriminate).
    apply not_absent_present in Hyna.
    destruct Hyna as [yc Hyna].
    exists (madd_obj obj menv''' menv'').
    exists (PM.add r yc env'').
    split; [|split].
    + (* TODO: automate! *)
      econstructor.
      exact H0.
      econstructor.
      exact H1.
      econstructor.
      exact Hfindo.
      econstructor.
      econstructor.
      exact Hfindc.
      apply Hstmt.
      apply Hout.
      apply Hyna.
      reflexivity.
      reflexivity.
    + (* TODO: automate! *)
      intuition.
      rewrite H2 in Hyna.
      injection Hyna; intro; subst.
      apply PM.gss.
      rewrite PM.gss in H2.
      injection H2; intro; subst.
      exact Hyna.
    + exists menv'''.
      intuition.
      apply mfind_inst_gss.
Qed.

Lemma is_translate_even_more_correct:
  forall (G: global)
         (f: ident)
         (xs: stream value)
         (ys: stream value)
         (ci: const)
         (r: ident)
         (obj: ident)
         (n: nat)
         (menv: heap)
         (env: stack),
    Welldef_global G
    -> sem_node G f xs ys
    -> (forall n, xs n = present ci)
    -> stmt_eval (translate G) hempty sempty
                 (Comp (Reset_ap f obj)
                       (Repeat (S n) (Step_ap r f obj (Const ci)))) (menv, env)
    -> (forall co, ys n = present co <-> PM.find r env = Some co).
Proof.
  intros G f xs ys ci r obj n menv env Hwdef Hsem Hin Hstmt.
  pose proof (is_translate_correct G f xs ys ci r obj n Hwdef Hsem Hin) as Hc.
  destruct Hc as [menv2 [env2 [Hstmt2 Hout]]].
  apply stmt_eval_det with (1:=Hstmt) in Hstmt2.
  injection Hstmt2; intros; subst.
  now apply Hout.
Qed.

